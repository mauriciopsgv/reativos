#include "/home/terra/TerraNet_v0.1/terra/TerraNet.defs"

var ushort nodeId = getNodeId();

pktype usrMsg from radioMsg with
	var ubyte[4] d8;
	var ushort[4] d16;
	var ulong[2] d32;
end

var ubyte distancia=0;
var ushort stat, pai=0;
var usrMsg	mensagem;


par/and do

	if(nodeId == 11) then
		pai = 1;
		mensagem.type = 1; 
		mensagem.source = nodeId;
		mensagem.target = BROADCAST;
		mensagem.d8[0] = distancia;
		emit SEND(mensagem);
		await SEND_DONE();
	end

with
	par/or do
		mensagem = await RECEIVE(1);
		if( pai == 0 ) then
				pai = mensagem.source;
				distancia = mensagem.d8[0] + 1;
				mensagem.d8[0] = distancia;
				mensagem.source = nodeId;
		
				emit SEND(mensagem);
				await SEND_DONE();	
		end
	with
		await 2s;
	end
end


loop do
	par/or do
		par/and do
				emit REQ_TEMP();
				mensagem.d16[0] = await TEMP();
				mensagem.type = 2;			
				mensagem.source = nodeId;
				mensagem.target = pai;				
				mensagem.d16[1] = nodeId;
				
				await (distancia)s;
	
				if(distancia > 8) then
					await (nodeId%10)s;		
				end
	
				stat = qPut(mensagem);
		with
			par/or do
				loop do
					mensagem = await RECEIVE(2);
					mensagem.source = nodeId;
		
					if( nodeId == 11) then
						mensagem.target = 1;
					else
						mensagem.target = pai;
					end
			
					stat = qPut(mensagem);
				end	
			with
				await 30s;
			end
		with
			par/or do	
				loop do
					await Q_READY;
		
					loop do
						if(qSize() > 0) then
							stat = qGet(mensagem);
							emit SEND_ACK(mensagem);
							await SEND_DONE_ACK(2);
						else
							break;
						end
					end	
				end
			with
				await 30s;
			end		
		end
	with
		await 30s;
	end	
end
